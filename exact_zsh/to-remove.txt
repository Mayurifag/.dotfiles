# There was a problem with pasted text with following whitespaces resulting that
# I can't use backspace or left arrow to navigate, etc.

# This speeds up pasting w/ autosuggest and handles enhancd plugin
# https://github.com/zsh-users/zsh-autosuggestions/issues/238
zsh_paste_enhancd_original_widget_exists=false
if (( $+widgets[enhancd-pre-paste] )); then
  zsh_paste_enhancd_original_widget_exists=true
fi
pasteinit() {
  if $zsh_paste_enhancd_original_widget_exists; then
    zle enhancd-pre-paste
  fi
  OLD_SELF_INSERT=${${(s.:.)widgets[self-insert]}[2,3]}
  zle -N self-insert url-quote-magic
}
pastefinish() {
  # Validate that OLD_SELF_INSERT still points to a valid widget
  if [[ -n "$OLD_SELF_INSERT" ]] && (( $+widgets[$OLD_SELF_INSERT] )); then
    zle -N self-insert $OLD_SELF_INSERT
  else
    # Fallback to default if widget is corrupted
    zle -N self-insert .self-insert
  fi

  if $zsh_paste_enhancd_original_widget_exists; then
    zle enhancd-post-paste
  fi
}
zstyle :bracketed-paste-magic paste-init pasteinit
zstyle :bracketed-paste-magic paste-finish pastefinish

# https://github.com/zsh-users/zsh-autosuggestions/issues/351
ZSH_AUTOSUGGEST_CLEAR_WIDGETS+=(bracketed-paste)

# Option 2: Disable fast-syntax-highlighting's special handling of pasted text.
# This prevents the weird coloring and cursor behavior you described.
_FSH_BRACKETED_PASTE_MAGIC=0
